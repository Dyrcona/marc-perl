#!/usr/bin/perl

use warnings;
use strict;

=head1 specs

Turns http://www.loc.gov/marc/bibliographic/ecbdist.html into the format
used by MARC::Lint.pm

Takes ecbdist.html as input.  Skips fixed fields and data marked
"[OBSOLETE]"  Also, the HTML file doesn't include the 841-88X tags,
so those are hardcoded here.

=head1 AUTHOR

Originally written by Colin Campbell at Sirsi, and taken over and modified
by Andy Lester.

=cut

open( my $fh, "../lib/MARC/Lint.pm" ) or die "Can't open module";
while ( <$fh> ) {
    print;
    last if /^__DATA__/;
}
close $fh;

local $/ = undef;
my $text = <>;
$text =~ s/(<BR>|\r|\n)+/\n/ig;
my @lines = split( /\n/, $text );


my $in_tag = undef;
my $i;
my $i1;
my $i2;
my $curr_indicator;

my $started = 0;
for ( @lines ) {
    unless ($started) {
	$started=1 if /Number and Code Fields/;
	next;
    }
    s/^\s+//;
    s/\s+$//;
    next if $_ eq "";

    #print ">> $_\n";
    if (/^(\d\d\d) -/) {
	my $tag = $1;
	if (/OBSOLETE/) { 
	    $in_tag = 0;
	    next; 
	}
	$in_tag = 1;
	my $r = /\(NR\)/ ? "NR" : "R";
	print "\n$tag\t$r\n";
	$i1 = $i2 = "";
	next;
    }

    next unless $in_tag;
    next if /OBSOLETE/;
    
    if (/^First/) {
	$curr_indicator = 1;
    } elsif (/^Second/) {
	print_indicator( 1, $i1 );
	$curr_indicator = 2;
    } elsif (/^Subfield/) {
	print_indicator( 2, $i2 );
	$curr_indicator = 0;
    } else {
	if ($curr_indicator) {
	    my $data = '';
	    if (/^(\d-\d)/) {
		$data = $1;
	    } elsif (/^([#0123456789])/) {
		$data = $1;
	    }
	    $data = "b" if $data eq "#";
	    if ($curr_indicator == 1) {
		$i1 .= $data;
	    } elsif ($curr_indicator == 2) {
		$i2 .= $data;
	    }

	} else {
	    if (/\$(.) -/) {
		print "$1\t";
		if (/\(R\)/) {
		    print "R\n";
		} else {
		    print "NR\n";
		}
	    }
	}
    }
} # main while

sub print_indicator {
    my $n = shift;
    my $val = shift;

    $val = "blank" if $val eq "b";

    print "ind$n\t$val\n";
}

# Dump the hardcoded ones that aren't in the HTML file.
print <DATA>;

__END__
850	R
ind1	blank
ind2	blank
a	R
8	R

852	R
ind1	b012345678
ind2	b012
a	NR
b	R
c	R
e	R
f	R
g	R
h	NR
i	R
j	NR
k	R
l	NR
m	R
n	NR
p	NR
q	NR
s	R
t	NR
x	R
z	R
2	NR
3	NR
6	NR
8	NR

856	R
ind1	b012347
ind2	b0128
a	R
b	R
c	R
d	R
f	R
h	NR
i	R
j	NR
k	NR
l	NR
m	R
n	NR
o	NR
p	NR
q	NR
r	NR
s	R
t	R
u	R
v	R
w	R
x	R
y	R
z	R
2	NR
3	NR
6	NR
8	R

880	R
ind1	
ind2	
6	NR

887	R
ind1	blank
ind2	blank
a	NR
2	NR
